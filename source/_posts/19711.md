---
layout: post
title: "PermissionController權限管理類別"
date: 2010-11-25 12:59:28
comments: true
categories: [VB.NET]
description: "PermissionController權限管理類別"
---
<p>由於權限控管功能很常被用到，因此又重新挖出了水瓶大Enum 的設計與應用 - 簡易權限設計這篇，想辦法整理一個不需更動又能重複使用的類別出來，最後結合泛型與反射做出了權限管理類別的雛型，這邊將該類別暫定為PermissionController。</p>  <p> </p>  <p>PermissionController能透過泛型決定要拿來做權限管理的權限列舉，該列舉需經Flag Attribute修飾過，並以2的次方做為列舉值的編碼。PermissionController建構時可將初始權限帶入，這時PermissionController會做一些條件上的判斷，像是列舉是否有經Flag Attribute修飾等，並會設定最高權限供後續內部使用。</p>  <p> </p>  <p>PermissionController程式碼如下：</p>  <div class="wlWriterSmartContent" id="scid:812469c5-0cb0-4c63-8c15-c81123a09de7:c2d77246-1d29-42c6-9550-9ffd55223912" style="padding-right: 0px; display: inline; padding-left: 0px; float: none; padding-bottom: 0px; margin: 0px; padding-top: 0px"><pre name="code" class="vb">''' &lt;summary&gt;
''' 
''' &lt;/summary&gt;
''' &lt;typeparam name="T"&gt;&lt;/typeparam&gt;
Public Class PermissionController(Of T)

#Region "Var"
    Private _allPermission As T
    Private _permission As T
#End Region


#Region "Public Property"
    Public Property AllPermission() As T
        Get
            Return _allPermission
        End Get
        Private Set(ByVal value As T)
            _allPermission = value
        End Set
    End Property

    ''' &lt;summary&gt;
    ''' Gets the permission.
    ''' &lt;/summary&gt;
    ''' &lt;value&gt;The permission.&lt;/value&gt;
    Public Property Permission() As T
        Get
            Return _permission
        End Get
        Private Set(ByVal value As T)
            _permission = value
        End Set
    End Property

#End Region

#Region "Constructor"
    ''' &lt;summary&gt;
    ''' Initializes a new instance of the &lt;see cref="PermissionController(Of T)" /&gt; class.
    ''' &lt;/summary&gt;
    ''' &lt;param name="permission"&gt;The permission.&lt;/param&gt;
    Public Sub New(ByVal permission As T)
        Dim permissionType As Type = GetType(T)

        If Not (permissionType.IsSubclassOf(GetType(System.Enum))) Then
            Throw New ArgumentException("permissionEnum must be a enum")
        End If

        If permissionType.GetCustomAttributes(GetType(FlagsAttribute), False).Length = 0 Then
            Throw New ArgumentException("permissionEnum must be a flag enum")
        End If

        LoadPermission(permission)
    End Sub
#End Region


#Region "Private Method"
    ''' &lt;summary&gt;
    ''' Prepares all permission.
    ''' &lt;/summary&gt;
    Private Sub PrepareAllPermission()
        Dim allPermissionValue As Integer = 0
        For Each value As Integer In [Enum].GetValues(Permission.[GetType]())
            allPermissionValue = allPermissionValue Or value
        Next
        AllPermission = ConvertToEnum(allPermissionValue)
    End Sub

    ''' &lt;summary&gt;
    ''' Converts to value.
    ''' &lt;/summary&gt;
    ''' &lt;param name="permission"&gt;The permission.&lt;/param&gt;
    ''' &lt;returns&gt;&lt;/returns&gt;
    Private Function ConvertToValue(ByVal permission As T) As Integer
        Return CInt(TryCast(permission, Object))
    End Function

    ''' &lt;summary&gt;
    ''' Converts to enum.
    ''' &lt;/summary&gt;
    ''' &lt;param name="permissionValue"&gt;The permission value.&lt;/param&gt;
    ''' &lt;returns&gt;&lt;/returns&gt;
    Private Function ConvertToEnum(ByVal permissionValue As Integer) As T
        Return DirectCast(TryCast(permissionValue, Object), T)
    End Function
#End Region

#Region "Protected Method"
    ''' &lt;summary&gt;
    ''' Returns a &lt;see cref="System.String" /&gt; that represents this instance.
    ''' &lt;/summary&gt;
    ''' &lt;returns&gt;
    ''' A &lt;see cref="System.String" /&gt; that represents this instance.
    ''' &lt;/returns&gt;
    Public Overrides Function ToString() As String
        Return Me.Permission.ToString()
    End Function
#End Region


#Region "Public Method"
    ''' &lt;summary&gt;
    ''' Loads the permission.
    ''' &lt;/summary&gt;
    ''' &lt;param name="permission"&gt;The permission.&lt;/param&gt;
    Public Sub LoadPermission(ByVal permission As T)
        Me.Permission = permission
        PrepareAllPermission()
    End Sub

    ''' &lt;summary&gt;
    ''' Adds the permission.
    ''' &lt;/summary&gt;
    ''' &lt;param name="permission"&gt;The permission.&lt;/param&gt;
    Public Sub AddPermission(ByVal permission As T)
        Me.Permission = ConvertToEnum(ConvertToValue(Me.Permission) Or ConvertToValue(permission))
    End Sub

    ''' &lt;summary&gt;
    ''' Removes the permission.
    ''' &lt;/summary&gt;
    ''' &lt;param name="permission"&gt;The permission.&lt;/param&gt;
    Public Sub RemovePermission(ByVal permission As T)
        Me.Permission = ConvertToEnum(ConvertToValue(Me.Permission) And (ConvertToValue(AllPermission) Xor ConvertToValue(permission)))
    End Sub



    ''' &lt;summary&gt;
    ''' Determines whether the specified permission contains permission.
    ''' &lt;/summary&gt;
    ''' &lt;param name="permission"&gt;The permission.&lt;/param&gt;
    ''' &lt;returns&gt;
    ''' &lt;c&gt;true&lt;/c&gt; if the specified permission contains permission; otherwise, &lt;c&gt;false&lt;/c&gt;.
    ''' &lt;/returns&gt;
    Public Function ContainsPermission(ByVal permission As T) As Boolean
        Return (ConvertToValue(Me.Permission) And ConvertToValue(permission)) = ConvertToValue(permission)
    End Function
#End Region

End Class</pre></div>

<p> </p>

<p>類別圖如下：</p>

<p><img style="border-right: 0px; border-top: 0px; border-left: 0px; border-bottom: 0px" height="250" alt="image" src="\images\posts\19711\image_thumb_1.png" width="202" border="0" /> </p>

<p> </p>

<p>裡面只有簡單的幾個方法與屬性，Permission屬性是當前的權限，AllPermission是最高權限，AddPermission是用來增加權限，ContainsPermission用來判斷是否有特定的權限，RemovePermission可移除特定權限，LoadPermission用以設定當前權限。</p>

<p> </p>

<p>完整的使用範例：</p>

<div class="wlWriterSmartContent" id="scid:812469c5-0cb0-4c63-8c15-c81123a09de7:e38e26b1-4534-4982-9983-91b85085784d" style="padding-right: 0px; display: inline; padding-left: 0px; float: none; padding-bottom: 0px; margin: 0px; padding-top: 0px"><pre name="code" class="vb">Module Module1

    &lt;Flags()&gt; _
    Enum Permission
        None = 0
        Create = 1
        Read = 2
    End Enum

    Sub Main()
        Dim PermissionAgent As New PermissionController(Of Permission)(Permission.Create)
        Console.WriteLine("所有權限: {0}", PermissionAgent.AllPermission.ToString)
        Console.WriteLine("當前權限: {0}", PermissionAgent.Permission.ToString)
        Console.WriteLine("是否含權限{0}: {1}", Permission.Read, PermissionAgent.ContainsPermission(Permission.Read))
        Console.WriteLine("加入權限{0}...", Permission.Read)
        PermissionAgent.AddPermission(Permission.Read)
        Console.WriteLine("當前權限: {0}", PermissionAgent.Permission.ToString)
        Console.WriteLine("是否含權限{0}: {1}", Permission.Read, PermissionAgent.ContainsPermission(Permission.Read))
        Console.WriteLine("移除權限{0}...", Permission.Read)
        PermissionAgent.RemovePermission(Permission.Read)
        Console.WriteLine("當前權限: {0}", PermissionAgent.Permission.ToString)
        Console.WriteLine("移除權限{0}...", Permission.Create)
        PermissionAgent.RemovePermission(Permission.Create)
        Console.WriteLine("當前權限: {0}", PermissionAgent.Permission.ToString)
    End Sub

End Module</pre></div>

<p> </p>

<p>運行結果：</p>

<p><img style="border-right: 0px; border-top: 0px; border-left: 0px; border-bottom: 0px" height="251" alt="image" src="\images\posts\19711\image_thumb_2.png" width="321" border="0" /> </p>

<p> </p>

<h2>Link</h2>

<ul>
  <li>Enum 的設計與應用 - 簡易權限設計</li>
</ul>