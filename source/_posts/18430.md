---
layout: post
title: "[VB.NET]用Extension Method取得CustomAttributes"
date: 2010-10-18 09:02:31
comments: true
tags: [VB.NET]
description: "[VB.NET]用Extension Method取得CustomAttributes"
---
<p>整理一下自己用來取得CustomAttributes的擴充方法。</p>  <p> </p>  <h2>EnumExtension</h2>  <div class="wlWriterSmartContent" id="scid:812469c5-0cb0-4c63-8c15-c81123a09de7:d8df6592-0972-4895-abcf-0fbf5882fac0" style="padding-right: 0px; display: inline; padding-left: 0px; float: none; padding-bottom: 0px; margin: 0px; padding-top: 0px"><pre name="code" class="vb">Imports System.Runtime.CompilerServices

Module EnumExtension

    &lt;Extension()&gt; _
    Function GetCustomAttributes(Of T)(ByVal e As [Enum]) As IEnumerable(Of T)
        Return e.GetType.GetField(e.ToString).GetCustomAttributes(GetType(T), False).Cast(Of T)()
    End Function

    &lt;Extension()&gt; _
    Function GetCustomAttribute(Of T)(ByVal e As [Enum]) As T
        Return GetCustomAttributes(Of T)(e).FirstOrDefault
    End Function

End Module</pre></div>

<p> </p>

<h2>TypeExtension</h2>

<div class="wlWriterSmartContent" id="scid:812469c5-0cb0-4c63-8c15-c81123a09de7:0ade4a91-f0e9-44ae-97a4-8a8127d65084" style="padding-right: 0px; display: inline; padding-left: 0px; float: none; padding-bottom: 0px; margin: 0px; padding-top: 0px"><pre name="code" class="vb">Imports System.Runtime.CompilerServices

Module TypeExtension

    &lt;Extension()&gt; _
    Function GetCustomAttributes(Of T)(ByVal type As Type) As IEnumerable(Of T)
        Return type.GetCustomAttributes(GetType(T), False).Cast(Of T)()
    End Function

    &lt;Extension()&gt; _
    Function GetCustomAttribute(Of T)(ByVal type As Type) As T
        Return DirectCast(GetCustomAttributes(Of T)(type).FirstOrDefault, T)
    End Function

    &lt;Extension()&gt; _
    Function GetCustomAttributes(Of T)(ByVal type As Type, ByVal memberName As String) As IEnumerable(Of T)
        Dim m = type.GetMember(memberName).FirstOrDefault
        If m Is Nothing Then
            Throw New MissingMemberException
        End If
        Return m.GetCustomAttributes(GetType(T), False)
    End Function

    &lt;Extension()&gt; _
    Function GetCustomAttribute(Of T)(ByVal type As Type, ByVal memberName As String) As T
        Return GetCustomAttributes(Of T)(type, memberName).FirstOrDefault
    End Function
End Module</pre></div>

<p>  </p>

<h2>ObjectExtension</h2>

<div class="wlWriterSmartContent" id="scid:812469c5-0cb0-4c63-8c15-c81123a09de7:7b57cbdc-1615-4d83-a4fc-6667db3efe1d" style="padding-right: 0px; display: inline; padding-left: 0px; float: none; padding-bottom: 0px; margin: 0px; padding-top: 0px"><pre name="code" class="vb">Imports System.Runtime.CompilerServices
Imports System.Reflection

Module ObjectExtension

    &lt;Extension()&gt; _
    Function GetCustomAttributes(Of T)(ByVal obj As Object) As IEnumerable(Of T)
        Return obj.GetType.GetCustomAttributes(GetType(T), False).Cast(Of T)()
    End Function

    &lt;Extension()&gt; _
    Function GetCustomAttribute(Of T)(ByVal obj As Object) As T
        Return GetCustomAttributes(Of T)(obj).FirstOrDefault
    End Function

    &lt;Extension()&gt; _
    Function GetCustomAttributes(Of T)(ByVal obj As Object, ByVal memberName As String) As IEnumerable(Of T)
        Dim m = obj.GetType.GetMember(memberName).FirstOrDefault
        If m Is Nothing Then
            Throw New MissingMemberException
        End If
        Return m.GetCustomAttributes(GetType(T), False)
    End Function

    &lt;Extension()&gt; _
    Function GetCustomAttribute(Of T)(ByVal obj As Object, ByVal memberName As String) As T
        Return GetCustomAttributes(Of T)(obj, memberName).FirstOrDefault
    End Function
End Module
</pre></div>

<p> </p>

<h2>使用範例</h2>

<div class="wlWriterSmartContent" id="scid:812469c5-0cb0-4c63-8c15-c81123a09de7:6953e5a4-36e0-4c2f-912a-0ead9917ab66" style="padding-right: 0px; display: inline; padding-left: 0px; float: none; padding-bottom: 0px; margin: 0px; padding-top: 0px"><pre name="code" class="vb">Module Module1

    Enum InterfaceType
        &lt;Description("RS232 Interface")&gt; _
        RS232

        &lt;Description("RS485 Interface")&gt; _
        RS485

        &lt;Description("GPIB Interface")&gt; _
        GPIB

        &lt;Description("I2C Interface")&gt; _
        I2C
    End Enum

    &lt;Description("Test Class")&gt; _
    Class TestClass

        &lt;Description("Test Property")&gt; _
        Property TestProperty As String

    End Class

    Sub Main()
        Dim c As New TestClass
        Console.WriteLine(InterfaceType.GPIB.GetCustomAttribute(Of DescriptionAttribute)().Description)
        Console.WriteLine(c.GetCustomAttribute(Of DescriptionAttribute).Description)
        Console.WriteLine(c.GetCustomAttribute(Of DescriptionAttribute)("TestProperty").Description)
    End Sub

End Module</pre></div>