---
layout: post
title: "[VB.NET]比對兩個目錄中不同的檔案"
date: 2010-04-13 11:35:46
comments: true
categories: [VB.NET]
description: "[VB.NET]比對兩個目錄中不同的檔案"
---
<p>這陣子回論壇問題時，試著使用交集的方式寫了比對檔案的函式，整理紀錄如下：</p>  <div style="padding-bottom: 0px; margin: 0px; padding-left: 0px; padding-right: 0px; display: inline; float: none; padding-top: 0px" id="scid:812469c5-0cb0-4c63-8c15-c81123a09de7:97656f56-4e75-4198-aff8-c2e2529d8a44" class="wlWriterEditableSmartContent"><pre name="code" class="vb:nocontrols">Imports System.IO
Imports System.Runtime.CompilerServices

Module DirectoryInfoExtension
    &lt;Extension()&gt; _
    Function GetDifferentFileList(ByVal sourceDirectoryInfo As DirectoryInfo, ByVal targetDirectoryInfo As DirectoryInfo, Optional ByVal searchPattern As String = "*.*", Optional ByVal searchOption As SearchOption = SearchOption.TopDirectoryOnly) As FileInfo()
        If sourceDirectoryInfo Is Nothing Then
            Throw New ArgumentNullException("sourceDirectoryInfo")
        End If
        If targetDirectoryInfo Is Nothing Then
            Throw New ArgumentNullException("targetDirectoryInfo")
        End If
        If targetDirectoryInfo.FullName = sourceDirectoryInfo.FullName Then
            Return New FileInfo() {}
        End If
        Dim sourceFiles = sourceDirectoryInfo.GetFiles(searchPattern, searchOption)
        Dim destFiles = targetDirectoryInfo.GetFiles(searchPattern, searchOption)
        Dim intersectFiles = sourceFiles.Except(destFiles, New FileInfoEqualityComparer(sourceDirectoryInfo.FullName, targetDirectoryInfo.FullName))
        Return intersectFiles.ToArray
    End Function
End Module

Class FileInfoEqualityComparer
    Implements IEqualityComparer(Of FileInfo)

#Region "Var"
    Private _sourcePath As String
    Private _targetPath As String
    Private _replacePathList() As String
#End Region

#Region "Private Property"
    ''' &lt;summary&gt; 
    ''' Gets or sets the m_source path. 
    ''' &lt;/summary&gt; 
    ''' &lt;value&gt;The m_source path.&lt;/value&gt; 
    Private Property m_SourcePath() As String
        Get
            Return _sourcePath
        End Get
        Set(ByVal value As String)
            _sourcePath = value
        End Set
    End Property


    ''' &lt;summary&gt; 
    ''' Gets or sets the m_target path. 
    ''' &lt;/summary&gt; 
    ''' &lt;value&gt;The m_target path.&lt;/value&gt; 
    Private Property m_TargetPath() As String
        Get
            Return _targetPath
        End Get
        Set(ByVal value As String)
            _targetPath = value
        End Set
    End Property

    ''' &lt;summary&gt; 
    ''' Gets or sets the m_replace path list. 
    ''' &lt;/summary&gt; 
    ''' &lt;value&gt;The m_replace path list.&lt;/value&gt; 
    Private Property m_replacePathList() As String()
        Get
            Return _replacePathList
        End Get
        Set(ByVal value As String())
            _replacePathList = value
        End Set
    End Property
#End Region

#Region "Constructor"

    ''' &lt;summary&gt; 
    ''' Initializes a new instance of the &lt;see cref="FileInfoEqualityComparer" /&gt; class. 
    ''' &lt;/summary&gt; 
    ''' &lt;param name="sourcePath"&gt;The source path.&lt;/param&gt; 
    ''' &lt;param name="targetPath"&gt;The target path.&lt;/param&gt; 
    Sub New(ByVal sourcePath As String, ByVal targetPath As String)
        With Me
            .m_SourcePath = sourcePath
            .m_TargetPath = targetPath
            InitReplacePathList()
        End With
    End Sub
#End Region

#Region "Private Method"
    ''' &lt;summary&gt; 
    ''' Inits the replace path list. 
    ''' &lt;/summary&gt; 
    Private Sub InitReplacePathList()
        m_replacePathList = New String() {m_SourcePath, m_TargetPath}
        If m_SourcePath &lt; m_TargetPath Then
            m_replacePathList = m_replacePathList.Reverse().ToArray
        End If
    End Sub

    ''' &lt;summary&gt; 
    ''' Gets the relatived path. 
    ''' &lt;/summary&gt; 
    ''' &lt;param name="path"&gt;The path.&lt;/param&gt; 
    ''' &lt;returns&gt;&lt;/returns&gt; 
    Private Function GetRelativedPath(ByVal path As String) As String
        If String.IsNullOrEmpty(path) Then
            Throw New ArgumentNullException("path")
        End If

        For Each p In m_replacePathList
            path = path.Replace(p, "")
        Next
        Return path
    End Function
#End Region

    Public Function Equals1(ByVal x As System.IO.FileInfo, ByVal y As System.IO.FileInfo) As Boolean Implements System.Collections.Generic.IEqualityComparer(Of System.IO.FileInfo).Equals
        Return GetRelativedPath(x.FullName) = GetRelativedPath(y.FullName) AndAlso x.Length = y.Length AndAlso x.LastWriteTime = y.LastWriteTime
    End Function

    Public Function GetHashCode1(ByVal obj As System.IO.FileInfo) As Integer Implements System.Collections.Generic.IEqualityComparer(Of System.IO.FileInfo).GetHashCode
        Return (GetRelativedPath(obj.FullName) &amp; obj.Length.ToString &amp; obj.LastWriteTime.ToShortDateString).GetHashCode
    End Function
End Class
</pre></div>

<p> </p>

<p>使用範例如下：</p>

<div style="padding-bottom: 0px; margin: 0px; padding-left: 0px; padding-right: 0px; display: inline; float: none; padding-top: 0px" id="scid:812469c5-0cb0-4c63-8c15-c81123a09de7:230680e6-4aa7-4c28-975f-b8ea4f78a303" class="wlWriterEditableSmartContent"><pre name="code" class="vb:nocontrols">Imports System.IO

Public Class Form1

    Private Sub btnSource_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles btnSource.Click
        If FolderBrowserDialog1.ShowDialog Then
            tbxSource.Text = FolderBrowserDialog1.SelectedPath
            Dim source As New DirectoryInfo(tbxSource.Text)
            lbxSource.DisplayMember = "FullName"
            lbxSource.Items.AddRange(source.GetFiles())
        End If
    End Sub

    Private Sub btnTarget_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles btnTarget.Click
        If FolderBrowserDialog1.ShowDialog Then
            tbxTarget.Text = FolderBrowserDialog1.SelectedPath
            Dim target As New DirectoryInfo(tbxTarget.Text)
            lbxTarget.DisplayMember = "FullName"
            lbxTarget.Items.AddRange(target.GetFiles())
        End If
    End Sub

    Private Sub btnGO_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles btnGO.Click
        Dim source As New DirectoryInfo(tbxSource.Text)
        Dim target As New DirectoryInfo(tbxTarget.Text)
        lbxDifferent.DisplayMember = "FullName"
        lbxDifferent.Items.AddRange(source.GetDifferentFileList(target))
    End Sub
End Class
</pre></div>

<p> </p>

<p>運行結果</p>

<p><img style="border-bottom: 0px; border-left: 0px; display: inline; border-top: 0px; border-right: 0px" title="image" border="0" alt="image" src="\images\posts\14572\image_thumb_1.png" width="457" height="329" /></p>