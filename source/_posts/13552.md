---
layout: post
title: "[VB.NET]調整TreeView或TreeNode下的節點"
date: 2010-02-09 10:10:25
comments: true
categories: [VB.NET]
description: "[VB.NET]調整TreeView或TreeNode下的節點"
---
<p> 整理一下網友問題。據網友開的需求，希望將本來長成像下面這樣的節點：   <br /> <img style="border-bottom: 0px; border-left: 0px; display: inline; border-top: 0px; border-right: 0px" title="image" border="0" alt="image" src="\images\posts\13552\image_thumb.png" width="270" height="226" /></a> </p>  <p> </p>  <p>整理成像下面這個樣子：   <br /><a href="http://files.dotblogs.com.tw/larrynung/1002/VB.NETTreeView_132FA/image_4.png" rel="lightbox"><img style="border-bottom: 0px; border-left: 0px; display: inline; border-top: 0px; border-right: 0px" title="image" border="0" alt="image" src="\images\posts\13552\image_thumb_1.png" width="270" height="226" /> </p>  <p> </p>  <p>這樣的需求我們可以先找出所有節點，找出後用巢狀迴圈去合併具有相同的FullPath的節點：   <br />    </p><div style="padding-bottom: 0px; margin: 0px; padding-left: 0px; padding-right: 0px; display: inline; float: none; padding-top: 0px" id="scid:812469c5-0cb0-4c63-8c15-c81123a09de7:a4b71ee5-0b33-41d3-aba4-1937be76453e" class="wlWriterEditableSmartContent"><pre name="code" class="vb:nocontrols">    Private Sub AdjustTreeNodes(ByVal tree As TreeView)
        Dim allNodes() As TreeNode = GetAllNodes(tree)
        Dim node1, node2 As TreeNode
        Try
            tree.SuspendLayout()
            tree.BeginUpdate()
            For idx1 As Integer = allNodes.Count - 1 To 0 Step -1
                For idx2 As Integer = idx1 - 1 To 0 Step -1
                    node1 = allNodes(idx1)
                    node2 = allNodes(idx2)
                    If node1.TreeView IsNot Nothing AndAlso node2.TreeView IsNot Nothing AndAlso node1.FullPath = node2.FullPath Then
                        Dim childNodes(node1.Nodes.Count - 1) As TreeNode
                        node1.Nodes.CopyTo(childNodes, 0)
                        node1.Remove()
                        node2.Nodes.AddRange(childNodes)
                    End If
                Next
            Next
        Finally
            tree.EndUpdate()
            tree.ResumeLayout()
        End Try
    End Sub

    Private Function GetAllNodes(ByVal treeOrNode As Object) As TreeNode()
        If Not TypeOf treeOrNode Is TreeNode AndAlso Not TypeOf treeOrNode Is TreeView Then
            Throw New ArgumentException("Error param type!!")
        End If

        Dim nodes As New List(Of TreeNode)
        If TypeOf treeOrNode Is TreeNode Then
            nodes.Add(treeOrNode)
        End If
        For Each tn As TreeNode In treeOrNode.Nodes
            nodes.AddRange(GetAllNodes(tn))
        Next
        Return nodes.ToArray
    End Function</pre></div>


<p> </p>

<p>使用上把TreeView或是TreeNode當作參數帶入即可，也可以整理成擴充方法使用：
  <br />  </p><div style="padding-bottom: 0px; margin: 0px; padding-left: 0px; padding-right: 0px; display: inline; float: none; padding-top: 0px" id="scid:812469c5-0cb0-4c63-8c15-c81123a09de7:73c9e346-8502-40cf-ac58-452f86b96ba2" class="wlWriterEditableSmartContent"><pre name="code" class="vb:nocontrols">Imports System.Runtime.CompilerServices

Public Module TreeViewExtension

#Region "Private Method"
    Private Function GetAllTreeNodes(ByVal treeOrNode As Object) As TreeNode()
        If Not TypeOf treeOrNode Is TreeNode AndAlso Not TypeOf treeOrNode Is TreeView Then
            Throw New ArgumentException("Error param type!!")
        End If

        Dim nodes As New List(Of TreeNode)
        If TypeOf treeOrNode Is TreeNode Then
            nodes.Add(treeOrNode)
        End If
        For Each tn As TreeNode In treeOrNode.Nodes
            nodes.AddRange(GetAllTreeNodes(tn))
        Next
        Return nodes.ToArray
    End Function

    Private Sub AdjustAllTreeNodes(ByVal treeOrNode As Object)
        Dim allNodes() As TreeNode = GetAllTreeNodes(treeOrNode)
        Dim node1, node2 As TreeNode
        For idx1 As Integer = allNodes.Count - 1 To 0 Step -1
            For idx2 As Integer = idx1 - 1 To 0 Step -1
                node1 = allNodes(idx1)
                node2 = allNodes(idx2)
                If node1.TreeView IsNot Nothing AndAlso node2.TreeView IsNot Nothing AndAlso node1.FullPath = node2.FullPath Then
                    Dim childNodes(node1.Nodes.Count - 1) As TreeNode
                    node1.Nodes.CopyTo(childNodes, 0)
                    node1.Remove()
                    node2.Nodes.AddRange(childNodes)
                End If
            Next
        Next
    End Sub
#End Region


#Region "Public Method"
    &lt;Extension()&gt; _
    Public Sub AdjustAllNodes(ByVal tree As TreeView)
        AdjustAllTreeNodes(tree)
    End Sub

    &lt;Extension()&gt; _
    Public Sub AdjustAllNodes(ByVal node As TreeNode)
        AdjustAllTreeNodes(node)
    End Sub
#End Region

End Module
</pre></div>